LOAD CSV WITH HEADERS FROM "file:///examples/northwind/products.csv" AS row
CREATE (n:Product)
SET n = row,
n.unitPrice = toFloat(row.unitPrice),
n.unitsInStock = toInteger(row.unitsInStock), n.unitsOnOrder = toInteger(row.unitsOnOrder),
n.reorderLevel = toInteger(row.reorderLevel), n.discontinued = (row.discontinued <> "0")



LOAD CSV WITH HEADERS FROM "file:///examples/northwind/categories.csv" AS row
CREATE (n:Category)
SET n = row



LOAD CSV WITH HEADERS FROM "file:///examples/northwind/suppliers.csv" AS row
CREATE (n:Supplier)
SET n = row



CREATE INDEX FOR (n:Product) ON (n.productID)



CREATE INDEX FOR (n:Category) ON (n.categoryID)



CREATE INDEX FOR (n:Supplier) ON (n.supplierID)



MATCH (p:Product),(c:Category)
WHERE p.categoryID = c.categoryID
CREATE (p)-[:PART_OF]->(c)




MATCH (p:Product),(s:Supplier)
WHERE p.supplierID = s.supplierID
CREATE (s)-[:SUPPLIES]->(p)


LOAD CSV WITH HEADERS FROM "file:///examples/northwind/customers.csv" AS row
CREATE (n:Customer)
SET n = row



LOAD CSV WITH HEADERS FROM "file:///examples/northwind/orders.csv" AS row
CREATE (n:Order)
SET n = row


CREATE INDEX FOR (n:Customer) ON (n.customerID)


CREATE INDEX FOR (n:Order) ON (n.orderID)

MATCH (c:Customer),(o:Order)
WHERE c.customerID = o.customerID
CREATE (c)-[:PURCHASED]->(o)


LOAD CSV WITH HEADERS FROM "file:///examples/northwind/order-details.csv" AS row
MATCH (p:Product), (o:Order)
WHERE p.productID = row.productID AND o.orderID = row.orderID
CREATE (o)-[details:ORDERS]->(p)
SET details = row,
details.quantity = toInteger(row.quantity)